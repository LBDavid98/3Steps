#class {flaab} {open}

#var guild jedi
#var subguild shimura

#nop {focus hpconfigure /HP: #hp/#mhp SP: #sp/#msp C: #c F: #f M: #sm/#%c D: #dn/#ng P: #df E: #es GXP: #gxp}
#gag {HP: %2/%3 SP: %4/%5 C: %6 F: %7 M: %8/%9 D:%10/%11 P: %12 E: %14GXP:%13}
#act {HP: %1/%2 SP: %3/%4 C: %5\% F: %6\% M: %7/%8\% D:%9/%10 P: %11 E:%12 GXP: %13\%} {
	#var {my[hp][current]}		{%1};
	#var {my[hp][max]}		{%2};
	#var {my[sp][current]}		{%3};
	#var {my[sp][max]}		{%4};
	#var {my[gp1][current]}		{%5};
	#var {my[gp2][current]}		{%6};
	#var {my[mode][current]}	{%7};
	#var {my[charge][current]}	{%8};
	#var {my[dissipate][current]}	{%9};
	#var {my[energy][current]}	{%10};
	#var {my[prots][current][all]}	{%11};
	#var {my[attacker]}		{%12};
	#var {my[gtnl]}			{%13};
	#math {my[hp][deficit]}{$my[hp][max] - $my[hp][current]};
	#math {my[sp][deficit]}{$my[sp][max] - $my[sp][current]};
        #if {"$my[attacker]" != " "}{
		#var {my[incombat]}	{1};
		#math {my[damagetracker][rounds]}{$my[damagetracker][rounds] + 1};
        }{
		#var {my[incombat]}	{0};
        };
	#if {$my[incombat] != 1}{
		#if {$my[sp][current] < $my[sp][max]}{
			!focus clear mind;
		};
		#if {$my[hp][current] < $my[hp][max]}{
			!focus reduce injury;
		};
	}{
		#if {$my[sp][deficit] > 50}{
			!focus clear mind;
		};
	};
        #regex {$my[prots][current][all]}{s}{
                #var {my[prots][current][lfs]}{1};
        }{
		#var {my[prots][current][lfs]}{0};
		!focus lesser force shield;
        };
        #regex {$my[prots][current][all]}{S}{
                #var {my[prots][current][gfs]}{1};
        }{
		#var {my[prots][current][gfs]}{0};
		!focus greater force shield;
        };
        #regex {$my[prots][current][all]}{D}{
                #var {my[prots][current][dodge]}{1};
        }{
		#var {my[prots][current][dodge]}{0};
		!focus dodge /on;
        };
        #regex {$my[prots][current][all]}{RP}{
                #var {my[prots][current][rpoison]}{1};
        }{
		#var {my[prots][current][rpoison]}{0};
		!focus resist poison;
        };
	#regex {$my[mode][current]}{h}{
		#nop;
	}{
		!focus hand to hand;
		!focus ka meditation for empowerment;
		!focus ka meditation for energy;
	};
	#regex {$mip[gline][2]}{Deflection UP}{
		#nop;
	}{
		!focus force deflection;
	};
	update_status;
}


#alias {update_status} {
        #if {"$cooler" == "on" || $corpses[cooler] > 0} {
                #format {sb1_cooler} {%+2s} {$corpses[cooler]};
                #var {sb1_cooler} { CL:<130>$sb1_cooler<088>}
        } {
                #var {sb1_cooler} {}
        };
        #if {"$freezer" == "on" || $corpses[freezer] > 0} {
                #format {sb2_freezer} {%+2s} {$corpses[freezer]};
                #var {sb2_freezer} { FZ:<130>$sb2_freezer<088>}
        } {
                #var {sb2_freezer} {}
        };
	#var	{my[hp][graph]}		{@graph{$my[hp][current];$my[hp][max]}};
	#var	{my[sp][graph]}		{@graph{$my[sp][current];$my[sp][max]}};
	#var	{my[gp1][graph]}	{@graph{$my[gp1][current];100}};
	#var	{my[gp2][graph]}	{@graph{$my[gp2][current];100}};
	#var	{my[charge][graph]}	{@graph{$my[charge][current];100}};
	#var	{enemy[graph]}		{@graph{$enemy[hp];100}};
	#format {hp}			{%+4s} {@colscale{$my[hp][current];$my[hp][max]}};
	#format {hp_max}		{%+4s} {<128>$my[hp][max]<088>};
	#format {sp}			{%+4s} {@colscale{$my[sp][current];$my[sp][max]}};
	#format {sp_max}		{%+4s} {<128>$my[sp][max]<088>};
	#format {conc}			{%+8s} {@colscale{$my[gp1][current];100}};
	#format {force}			{%+6s} {@colscale{$my[gp2][current];100}};
	#format {charge}		{%+6s} {@colscale{$my[charge][current];100}};
	#format {ehealth}		{%+8s} {@colscale{$enemy[hp];100}\%};
        #format {corpses[inventory]}    {%+2s} {$corpses[inventory]};
        #format {corpses[truck]}	{%+2s} {$corpses[truck]};
        #format {corpses[coffin]}	{%+2s} {$corpses[coffin]};
        #format {corpses[freezer]}	{%+2s} {$corpses[freezer]};
	#showme {<088>HP:${hp}/${hp_max} SP:${sp}/${sp_max} C:${conc}%  F:${force}%    C: ${charge}%}{-4};
	#showme {H:${my[hp][graph]} S:${my[sp][graph]} C:${my[gp1][graph]} F:${my[gp2][graph]} C: ${my[charge][graph]}E:${enemy[graph]}} {-3};
	#showme {$mip[gline][2]} {-6};
	#showme {$mip[gline][1]} {-7};
}

#act {Flaab dealt the killing blow to %1.} {
	log_damage_no_numbers %1;
	#if {"$my[autocorpse][method]" != "off"}{
		corpsetrig;
	};
}

#class {flaab} {close}
